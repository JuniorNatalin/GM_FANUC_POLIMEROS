/PROG  STYLE103_TC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TC PK NEST 35";
PROG_SIZE	= 2670;
CREATE		= DATE 19-03-05  TIME 17:31:32;
MODIFIED	= DATE 22-03-31  TIME 06:11:22;
FILE_NAME	= PRT104_R;
VERSION		= 0;
LINE_COUNT	= 112;
MEMORY_SIZE	= 3134;
PROTECT		= READ_WRITE;
STORAGE		= SHADOW;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  !* LINE: Line 1                 * ;
   5:  !* ROBOT: RX1                   * ;
   6:  !******************************** ;
   7:  !******************************** ;
   8:  ! STYLE107: PROGRAM FOR MULTI ;
   9:  !    TOOL PICK TO FAR NEST ;
  10:  ! FAR = NEST 1 OR STA 50 ;
  11:  !******************************** ;
  12:  !  2016-08-01 Global-4 S/W Rel 4. ;
  13:  !******************************** ;
  14:  ! Author- Esys Automation ;
  15:  !******************************** ;
  16:   ;
  17:  ! INITIALIZE ;
  18:  GO[1:0]=103 ;
  19:  R[14]=100    ;
  20:  R[139]=1    ;
  21:  R[140]=1    ;
  22:   ;
  23:  !-------------------------------- ;
  24:  LBL[10:TOOL CHECKS] ;
  25:   ;
  26:  !Check if No Tool Present ;
  27:  R[133]=0    ;
  28:  CHK TOOL PRESENT(0) ;
  29:   ;
  30:  !If No TL Present, Cont to Pounce ;
  31:  IF R[133]=1,JMP LBL[20] ;
  32:   ;
  33:  !If TL Present, MHFault ;
  34:  JMP LBL[905] ;
  35:   ;
  36:  !-------------------------------- ;
  37:  LBL[20:MOVE TO POUNCE] ;
  38:   ;
  39:  CALL POUNCE_STA35    ;
  40:   ;
  41:  !Get Tool ID From PLC ;
  42:  R[156]=GI[70:38]    ;
  43:   ;
  44:  ! Move to Home or Continue Cycle ;
  45:  SELECT R[150]=0,JMP LBL[30] ;
  46:         =15,JMP LBL[299] ;
  47:         ELSE,JMP LBL[20] ;
  48:   ;
  49:  !-------------------------------- ;
  50:  LBL[30:START TOOL PICK] ;
  51:  !Pick Tool to far nest 35 ;
  52:  ! based on carried tool ;
  53:  IF (GI[15:38]=255) THEN ;
  54:  !Pick Tool Nest 35 ;
  55:  CALL S103_PK_TCOMUM35_R01    ;
  56:  ELSE ;
  57:  MESSAGE[AGUARDA ROBO SEM TOOL ] ;
  58:  WAIT GI[15:38]=255    ;
  59:  JMP LBL[30] ;
  60:  ENDIF ;
  61:   ;
  62:  LBL[299] ;
  63:  !SEG 63, Robot Move to Home ;
  64:  SET SEGMENT(63) ;
  65:   ;
  66:  CALL RET_HOME_STA35    ;
  67:   ;
  68:  MOVE TO HOME    ;
  69:   ;
  70:  EXIT I-ZONE(1) ;
  71:  EXIT I-ZONE(2) ;
  72:   ;
  73:  !Check if Tool Present ;
  74:  R[133]=0    ;
  75:  CHK TOOL PRESENT(1) ;
  76:   ;
  77:  !Update Tool Information ;
  78:  R[157]=GI[15:38]    ;
  79:  IF (GI[15:38]>0 AND GI[15:38]<=50) THEN ;
  80:  GO[70:1]=1 ;
  81:  ENDIF ;
  82:  END ;
  83:   ;
  84:   ;
  85:  !*****FAULTS******** ;
  86:  LBL[905:Invalid Tool Num] ;
  87:  MESSAGE[...] ;
  88:  MESSAGE[TOOL CHANGER FAULT] ;
  89:  MESSAGE[ INVALID TOOL NUMBER] ;
  90:  MESSAGE[ TOOL LATCHED] ;
  91:  MESSAGE[ GI[15] MUST ] ;
  92:  MESSAGE[ EQUAL 255 ] ;
  93:  DO[523:OFF]=ON ;
  94:  UALM[3] ;
  95:  DO[523:OFF]=OFF ;
  96:  JMP LBL[10] ;
  97:   ;
  98:  LBL[910:Invalid Code] ;
  99:  MESSAGE[...] ;
 100:  MESSAGE[TOOL CHANGER FAULT] ;
 101:  MESSAGE[ INVALID TOOL NUMBER] ;
 102:  MESSAGE[ R[156] NOT IN SELECT] ;
 103:  MESSAGE[ LIST.] ;
 104:  MESSAGE[ CHECK PLC OR ] ;
 105:  MESSAGE[ TEACH PICK PATH FOR] ;
 106:  MESSAGE[ CARRIED TOOL.] ;
 107:  MESSAGE[...] ;
 108:  DO[523:OFF]=ON ;
 109:  UALM[3] ;
 110:  DO[523:OFF]=OFF ;
 111:  JMP LBL[10] ;
 112:   ;
/POS
/END
