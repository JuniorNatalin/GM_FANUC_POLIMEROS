/PROG  STYLE105_TC_R01
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TC DR N1";
PROG_SIZE	= 3880;
CREATE		= DATE 18-05-04  TIME 18:24:26;
MODIFIED	= DATE 19-07-18  TIME 14:31:02;
FILE_NAME	= PRT101R1;
VERSION		= 0;
LINE_COUNT	= 176;
MEMORY_SIZE	= 4540;
PROTECT		= READ_WRITE;
STORAGE		= SHADOW;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  !* LINE: Line 1                 * ;
   5:  !* ROBOT: RX1                   * ;
   6:  !******************************** ;
   7:  !******************************** ;
   8:  ! STYLE105: PROGRAM FOR MULTI ;
   9:  !    TOOL DROP TO NEAR NEST ;
  10:  ! NEAR = NEST 1 OR STA 50 ;
  11:  !******************************** ;
  12:  !  2016-08-01 Global-4 S/W Rel 4. ;
  13:  !******************************** ;
  14:  ! Author- Esys Automation ;
  15:  !******************************** ;
  16:   ;
  17:  ! INITIALIZE ;
  18:  GO[1:0]=101 ;
  19:  R[14]=100    ;
  20:  R[139]=1    ;
  21:  R[140]=1    ;
  22:  PAYLOAD[2] ;
  23:   ;
  24:  !-------------------------------- ;
  25:  LBL[10:TOOL CHECKS] ;
  26:   ;
  27:  !Check if Tool Present ;
  28:  R[133]=0    ;
  29:  CHK TOOL PRESENT(1) ;
  30:   ;
  31:  !Get Tool ID From PLC ;
  32:  R[156]=GI[15:38]    ;
  33:   ;
  34:  !Check if Correct Tool for Drop ;
  35:  R[133]=0    ;
  36:  CALL TL_CHK_KM(R[156]) ;
  37:   ;
  38:  !If TL CHK Pass, Cont to Pounce ;
  39:  IF R[133]=1,JMP LBL[20] ;
  40:   ;
  41:  !If TL CHK Fail, MHFault ;
  42:  JMP LBL[905] ;
  43:   ;
  44:  !-------------------------------- ;
  45:  LBL[20:MOVE TO POUNCE] ;
  46:   ;
  47:  CALL POUNC_TL    ;
  48:   ;
  49:  ! Move to Home or Continue Cycle ;
  50:  SELECT R[150]=0,JMP LBL[30] ;
  51:         =15,JMP LBL[299] ;
  52:         ELSE,JMP LBL[20] ;
  53:   ;
  54:  !-------------------------------- ;
  55:  LBL[30:START TOOL DROP] ;
  56:   ;
  57:  !Drop Tool to near nest 050 ;
  58:  ! based on carried tool ;
  59:  SELECT R[156]=1,JMP LBL[101] ;
  60:         =2,JMP LBL[102] ;
  61:         =3,JMP LBL[103] ;
  62:         =4,JMP LBL[104] ;
  63:         =5,JMP LBL[105] ;
  64:         =6,JMP LBL[106] ;
  65:         =7,JMP LBL[107] ;
  66:         =8,JMP LBL[108] ;
  67:         =9,JMP LBL[109] ;
  68:         =10,JMP LBL[110] ;
  69:         =11,JMP LBL[111] ;
  70:         =31,JMP LBL[131] ;
  71:         =15,JMP LBL[115] ;
  72:         =42,JMP LBL[142] ;
  73:   ;
  74:         ELSE,JMP LBL[910] ;
  75:   ;
  76:  !------------------- ;
  77:  LBL[101] ;
  78:  !Drop Tool 1 Nest 1 ;
  79:  CALL S105_DR_T01N1_R01    ;
  80:  JMP LBL[299] ;
  81:   ;
  82:  !------------------- ;
  83:  LBL[102] ;
  84:  !Drop Tool 2 Nest 1 ;
  85:  CALL ...    ;
  86:  JMP LBL[299] ;
  87:   ;
  88:  !------------------- ;
  89:  LBL[103] ;
  90:  !Drop Tool 3 Nest 1 ;
  91:  CALL S105_DR_T03N1_R01    ;
  92:  JMP LBL[299] ;
  93:   ;
  94:  LBL[105] ;
  95:  !Drop Tool 5 Nest 1 ;
  96:  CALL S105_DR_T05N1_R01    ;
  97:  JMP LBL[299] ;
  98:   ;
  99:  LBL[106] ;
 100:  !Drop Tool 6 Nest 1 ;
 101:  CALL S105_DR_T06N1_R01    ;
 102:  JMP LBL[299] ;
 103:   ;
 104:  LBL[107] ;
 105:  !Drop Tool 7 Nest 1 ;
 106:  CALL S105_DR_T07N1_R01    ;
 107:  JMP LBL[299] ;
 108:   ;
 109:  LBL[109] ;
 110:  !Drop Tool 9 Nest 1 ;
 111:  CALL S105_DR_T09N1_R01    ;
 112:  JMP LBL[299] ;
 113:   ;
 114:  LBL[115] ;
 115:  !Drop Tool 15 Nest 1 ;
 116:  CALL S105_DR_T15N1_R01    ;
 117:  JMP LBL[299] ;
 118:   ;
 119:  LBL[131] ;
 120:  !Drop Tool 15 Nest 1 ;
 121:  CALL S105_DR_T31N1_R01    ;
 122:  JMP LBL[299] ;
 123:   ;
 124:  LBL[142] ;
 125:  !Drop Tool 42 Nest 1 ;
 126:  CALL S105_DR_T42N1_R01    ;
 127:  JMP LBL[299] ;
 128:   ;
 129:   ;
 130:  !-------------------------------- ;
 131:  LBL[299:Return Home] ;
 132:   ;
 133:  !SEG 63, Robot Move to Home ;
 134:  SET SEGMENT(63) ;
 135:   ;
 136:  MOVE TO HOME    ;
 137:   ;
 138:  !Check if No Tool Present ;
 139:  R[133]=0    ;
 140:  CHK TOOL PRESENT(0) ;
 141:   ;
 142:  !Update Tool Information ;
 143:  R[157]=GI[15:38]    ;
 144:  GO[70:1]=0 ;
 145:  END ;
 146:   ;
 147:   ;
 148:  !*****FAULTS******** ;
 149:  LBL[905:Invalid Tool Num] ;
 150:  MESSAGE[...] ;
 151:  MESSAGE[TOOL CHANGER FAULT] ;
 152:  MESSAGE[ INVALID TOOL NUMBER] ;
 153:  MESSAGE[ GI[70] MUST BE ] ;
 154:  MESSAGE[ EQUAL GI[15] ] ;
 155:  DO[523:OFF]=ON ;
 156:  UALM[3] ;
 157:  DO[523:OFF]=OFF ;
 158:  JMP LBL[10] ;
 159:   ;
 160:  LBL[910:Invalid Code] ;
 161:  MESSAGE[...] ;
 162:  MESSAGE[TOOL CHANGER FAULT] ;
 163:  MESSAGE[ INVALID TOOL NUMBER] ;
 164:  MESSAGE[ R[156] NOT IN SELECT] ;
 165:  MESSAGE[ LIST.] ;
 166:  MESSAGE[ GI[70:NextToolID] ] ;
 167:  MESSAGE[ NOT EQUAL TO CARRIED TOOL] ;
 168:  MESSAGE[ CHECK PLC OR ] ;
 169:  MESSAGE[ TEACH DROP PATH FOR] ;
 170:  MESSAGE[ CARRIED TOOL.] ;
 171:  MESSAGE[...] ;
 172:  DO[523:OFF]=ON ;
 173:  UALM[3] ;
 174:  DO[523:OFF]=OFF ;
 175:  JMP LBL[10] ;
 176:   ;
/POS
/END
