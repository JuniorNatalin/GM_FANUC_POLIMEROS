/PROG  TL_UNLCH	  Macro
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "UNLATCH TOOL PLT";
PROG_SIZE	= 2844;
CREATE		= DATE 07-02-12  TIME 08:04:18;
MODIFIED	= DATE 16-10-26  TIME 11:33:22;
FILE_NAME	= TL_UNLCH;
VERSION		= 0;
LINE_COUNT	= 118;
MEMORY_SIZE	= 3284;
PROTECT		= READ;
STORAGE		= CMOS;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  ! TL_UNLCH: MACRO FOR UNLATCHING ;
   5:  !    THE TOOL PLATE ;
   6:  !******************************** ;
   7:  ! 2016-10-26 Global-4 S/W Rel 4.0 ;
   8:  !******************************** ;
   9:   ;
  10:  CALL DMSTAT    ;
  11:   ;
  12:  IF GO[3:0]=50 OR GO[3:0]=52,JMP LBL[10] ;
  13:  JMP LBL[55] ;
  14:   ;
  15:  LBL[10] ;
  16:  R[136]=DI[276:ON ]    ;
  17:   ;
  18:  !AUX POWER MUST BE ON IF SUPPORTD ;
  19:  ! BY QC HARDWARE, I.E. R[141]=1 ;
  20:  IF R[141]=1 AND DI[2008:ON ]=OFF,JMP LBL[50] ;
  21:   ;
  22:  IF R[145]=0,JMP LBL[15] ;
  23:  ! OPEN ALL GRIPPER VALVES ;
  24:  CALL OPN_AVLE    ;
  25:   ;
  26:  LBL[15] ;
  27:   ;
  28:  LBL[20] ;
  29:   ;
  30:  WAIT    .50(sec) ;
  31:  IF (!SI[9:ON ]),CALL CUSTOOK(19) ;
  32:  IF (DI[907:OFF] AND DI[909:ON ]),DO[905:OFF]=(OFF) ;
  33:  IF (DI[908:OFF] AND DI[909:ON ]),DO[906:OFF]=PULSE,1.0sec ;
  34:  WAIT   1.50(sec) ;
  35:   ;
  36:  IF DI[905:ON ]=OFF AND DI[906:OFF]=ON,JMP LBL[90] ;
  37:   ;
  38:  !================================ ;
  39:  ! UNABLE TO UNLATCH FAULT ;
  40:  MESSAGE[...] ;
  41:  MESSAGE[TL_UNLCH: TC FAULT ...] ;
  42:  MESSAGE[ UNABLE TO UNLATCH.] ;
  43:  MESSAGE[ NEED DI[906] HIGH] ;
  44:  MESSAGE[ AND DI[905] LOW.] ;
  45:   ;
  46:  R[170]=0    ;
  47:  IF GO[71:0]=126,JMP LBL[99] ;
  48:   ;
  49:  IF R[167]=1,JMP LBL[100] ;
  50:  JMP LBL[60] ;
  51:   ;
  52:  !================================ ;
  53:  ! AUX POWER FAULT ;
  54:  LBL[50:AUX POWER FAULT] ;
  55:  MESSAGE[...] ;
  56:  MESSAGE[TL_UNLCH: TC FAULT ...] ;
  57:  MESSAGE[ AUX POWER FAULT -] ;
  58:  MESSAGE[ NO AUX POWER AT ROBOT] ;
  59:  MESSAGE[ SIDE OF TOOL CHANGER.] ;
  60:  MESSAGE[ NEED DI[2008] HIGH.] ;
  61:   ;
  62:  R[170]=0    ;
  63:  IF GO[71:0]=126,JMP LBL[99] ;
  64:   ;
  65:  IF R[167]=1,JMP LBL[100] ;
  66:  JMP LBL[60] ;
  67:   ;
  68:  !================================ ;
  69:  LBL[55:CANNOT UNLATCH] ;
  70:  MESSAGE[...] ;
  71:  MESSAGE[TL_UNLCH: ROBOT NOT IN] ;
  72:  MESSAGE[CORRECT PATH SEGMENT] ;
  73:  MESSAGE[UNLATCH IS NOT ALLOWED] ;
  74:  MESSAGE[SHOULD HAVE PATH SEGMT] ;
  75:  MESSAGE[GO[3] EQUAL 50 OR 52.] ;
  76:  R[170]=0    ;
  77:  IF GO[71:0]=126,JMP LBL[99] ;
  78:   ;
  79:  IF R[167]=1,JMP LBL[100] ;
  80:  JMP LBL[60] ;
  81:   ;
  82:  !================================ ;
  83:  LBL[60:FAULT RECOVERY] ;
  84:   ;
  85:  SELECT R[136]=1,JMP LBL[70] ;
  86:         =0,JMP LBL[80] ;
  87:         ELSE,JMP LBL[60] ;
  88:   ;
  89:  !-------------------------------- ;
  90:  LBL[70:HMI RECOVERY] ;
  91:  MESSAGE[HMI FAULT RECOVERY] ;
  92:  MH FAULT RECOVRY(109,1,4) ;
  93:  JMP LBL[100] ;
  94:   ;
  95:  !-------------------------------- ;
  96:  LBL[80:TP RECOVERY] ;
  97:  MESSAGE[TP FAULT RECOVERY] ;
  98:  MH FAULT RECOVRY(110,1,4) ;
  99:  JMP LBL[100] ;
 100:   ;
 101:  !================================ ;
 102:  LBL[90:TOOL UNLATCH OK] ;
 103:  DO[338:ON ]=OFF ;
 104:  R[132]=0    ;
 105:  IF GO[71:0]=126,JMP LBL[100] ;
 106:  R[170]=1    ;
 107:  JMP LBL[100] ;
 108:   ;
 109:  !================================ ;
 110:  LBL[99:ABORT] ;
 111:  MESSAGE[...] ;
 112:  MESSAGE[TL_UNLCH: LATCHING FLT] ;
 113:  MESSAGE[UNABLE TO RECOVER] ;
 114:  MESSAGE[ABORT PROGRAM ...] ;
 115:  MH FAULT RECOVRY(116,1,0) ;
 116:  ABORT ;
 117:   ;
 118:  LBL[100:END OF PROGRAM] ;
/POS
/END
