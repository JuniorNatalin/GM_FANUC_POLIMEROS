/PROG  STYLE101_TC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TC DR NEST 35";
PROG_SIZE	= 2826;
CREATE		= DATE 19-03-05  TIME 17:30:32;
MODIFIED	= DATE 22-01-22  TIME 14:28:06;
FILE_NAME	= PRT101R1;
VERSION		= 0;
LINE_COUNT	= 119;
MEMORY_SIZE	= 3270;
PROTECT		= READ_WRITE;
STORAGE		= SHADOW;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  !* LINE: Line 1                 * ;
   5:  !* ROBOT: RX1                   * ;
   6:  !******************************** ;
   7:  !******************************** ;
   8:  ! STYLE102: PROGRAM FOR MULTI ;
   9:  !    TOOL DROP TO NEAR NEST ;
  10:  ! NEAR = NEST 1 OR STA 50 ;
  11:  !******************************** ;
  12:  !  2016-08-01 Global-4 S/W Rel 4. ;
  13:  !******************************** ;
  14:  ! Author- Esys Automation ;
  15:  !******************************** ;
  16:   ;
  17:  ! INITIALIZE ;
  18:  GO[1:0]=101 ;
  19:  R[14]=100    ;
  20:  R[139]=1    ;
  21:  R[140]=1    ;
  22:  PAYLOAD[2] ;
  23:   ;
  24:  !-------------------------------- ;
  25:  LBL[10:TOOL CHECKS] ;
  26:   ;
  27:  !Check if Tool Present ;
  28:  CHK TOOL PRESENT(1) ;
  29:   ;
  30:  !Get Tool ID From PLC ;
  31:  R[156]=GI[15:38]    ;
  32:   ;
  33:  !Check if Correct Tool for Drop ;
  34:  R[133]=0    ;
  35:  CALL TL_CHK_KM(R[156]) ;
  36:   ;
  37:  !If TL CHK Pass, Cont to Pounce ;
  38:  IF R[133]=1,JMP LBL[20] ;
  39:   ;
  40:  !If TL CHK Fail, MHFault ;
  41:  JMP LBL[905] ;
  42:   ;
  43:  !-------------------------------- ;
  44:  LBL[20:MOVE TO POUNCE] ;
  45:   ;
  46:  ENTER I-ZONE(1) ;
  47:  ENTER I-ZONE(2) ;
  48:   ;
  49:  CALL POUNCE_STA35    ;
  50:   ;
  51:  ! Move to Home or Continue Cycle ;
  52:  SELECT R[150]=0,JMP LBL[30] ;
  53:         =15,JMP LBL[299] ;
  54:         ELSE,JMP LBL[20] ;
  55:   ;
  56:  !-------------------------------- ;
  57:  LBL[30:START TOOL DROP] ;
  58:   ;
  59:  !Drop Tool to near nest 35 ;
  60:  ! based on carried tool ;
  61:  IF (GI[15:38]>=1 AND GI[15:38]<=50) THEN ;
  62:  !Drop Tool Nest 35 ;
  63:  CALL S101_DR_TCOMUM35_R01    ;
  64:  ELSE ;
  65:  MESSAGE[AGUARDA TOOL CORRETA] ;
  66:  WAIT GI[15:38]>=1 AND GI[15:38]<=50    ;
  67:  JMP LBL[30] ;
  68:  ENDIF ;
  69:   ;
  70:  LBL[299] ;
  71:  !SEG 63, Robot Move to Home ;
  72:  SET SEGMENT(63) ;
  73:   ;
  74:  CALL RET_HOME_STA35    ;
  75:   ;
  76:  MOVE TO HOME    ;
  77:   ;
  78:  EXIT I-ZONE(1) ;
  79:  EXIT I-ZONE(2) ;
  80:   ;
  81:  !Check if No Tool Present ;
  82:  R[133]=0    ;
  83:  CHK TOOL PRESENT(0) ;
  84:   ;
  85:  !Update Tool Information ;
  86:  R[157]=GI[15:38]    ;
  87:  GO[70:1]=0 ;
  88:  END ;
  89:   ;
  90:   ;
  91:  !*****FAULTS******** ;
  92:  LBL[905:Invalid Tool Num] ;
  93:  MESSAGE[...] ;
  94:  MESSAGE[TOOL CHANGER FAULT] ;
  95:  MESSAGE[ INVALID TOOL NUMBER] ;
  96:  MESSAGE[ GI[70] MUST BE ] ;
  97:  MESSAGE[ EQUAL GI[15] ] ;
  98:  DO[523:OFF]=ON ;
  99:  UALM[3] ;
 100:  DO[523:OFF]=OFF ;
 101:  JMP LBL[10] ;
 102:   ;
 103:  LBL[910:Invalid Code] ;
 104:  MESSAGE[...] ;
 105:  MESSAGE[TOOL CHANGER FAULT] ;
 106:  MESSAGE[ INVALID TOOL NUMBER] ;
 107:  MESSAGE[ R[156] NOT IN SELECT] ;
 108:  MESSAGE[ LIST.] ;
 109:  MESSAGE[ GI[70:NextToolID] ] ;
 110:  MESSAGE[ NOT EQUAL TO CARRIED TOOL] ;
 111:  MESSAGE[ CHECK PLC OR ] ;
 112:  MESSAGE[ TEACH DROP PATH FOR] ;
 113:  MESSAGE[ CARRIED TOOL.] ;
 114:  MESSAGE[...] ;
 115:  DO[523:OFF]=ON ;
 116:  UALM[3] ;
 117:  DO[523:OFF]=OFF ;
 118:  JMP LBL[10] ;
 119:   ;
/POS
/END
