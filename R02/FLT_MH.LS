/PROG  FLT_MH	  Macro
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "MH FLT & RECOVRY";
PROG_SIZE	= 5774;
CREATE		= DATE 06-12-07  TIME 07:26:20;
MODIFIED	= DATE 16-10-26  TIME 13:51:52;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 288;
MEMORY_SIZE	= 6466;
PROTECT		= READ;
STORAGE		= CMOS;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  ! FLT_RCVR: MACRO FOR HANDLING ;
   5:  ! M/H AND TOOL CHANGE FAULTS AND ;
   6:  ! ALERTS AND RECOVERY OPTIONS. ;
   7:  ! AR[1]: Fault Message Code ;
   8:  ! AR[2]: 1 if Fault, 2 if Alert ;
   9:  ! AR[3]: Response Dialog ;
  10:  !******************************** ;
  11:  ! 2016-05-03 GLOBAL-4 S/W Rel 4.0 ;
  12:  !******************************** ;
  13:   ;
  14:  $WAITTMOUT=50 ;
  15:   ;
  16:  R[136]=DI[276]    ;
  17:  R[164]=AR[1]    ;
  18:  R[163]=AR[2]    ;
  19:  R[169]=AR[3]    ;
  20:  GO[71]=R[164] ;
  21:  R[162]=0    ;
  22:  R[167]=0    ;
  23:  R[168]=0    ;
  24:   ;
  25:  LBL[5] ;
  26:  IF R[163]=2,JMP LBL[95] ;
  27:   ;
  28:  DO[523]=ON ;
  29:  SELECT R[169]=1,JMP LBL[10] ;
  30:         =2,JMP LBL[20] ;
  31:         =3,JMP LBL[30] ;
  32:         =4,JMP LBL[40] ;
  33:         =5,JMP LBL[80] ;
  34:         ELSE,JMP LBL[97] ;
  35:   ;
  36:  !-------------------------------- ;
  37:  !PART_LOST FLT MSG & RECOVERY ;
  38:  !RETRY_IGNORE_DROP DIALOG ;
  39:  LBL[10:PRT LOST FLT RCV] ;
  40:  IF R[136]=1,JMP LBL[19] ;
  41:   ;
  42:  CALL PCUSTLST(1,162) ;
  43:   ;
  44:  SELECT R[162]=1,JMP LBL[11] ;
  45:         =2,JMP LBL[12] ;
  46:         =3,JMP LBL[13] ;
  47:         ELSE,JMP LBL[14] ;
  48:   ;
  49:  LBL[19:REMOTE RESPONSE] ;
  50:  WAIT GI[72]>0    ;
  51:  R[162]=GI[72]    ;
  52:  SELECT R[162]=2,JMP LBL[11] ;
  53:         =3,JMP LBL[12] ;
  54:         =4,JMP LBL[13] ;
  55:         ELSE,JMP LBL[14] ;
  56:   ;
  57:  LBL[11:RETRY] ;
  58:  R[167]=1    ;
  59:  CHK PART PRESENT(1) ;
  60:  IF R[147]=0,JMP LBL[10] ;
  61:  R[168]=2    ;
  62:  JMP LBL[80] ;
  63:   ;
  64:  LBL[12:IGNORE] ;
  65:  R[165]=1    ;
  66:  R[162]=0    ;
  67:  R[168]=3    ;
  68:  JMP LBL[80] ;
  69:   ;
  70:  LBL[13:DROP] ;
  71:  R[165]=1    ;
  72:  R[162]=0    ;
  73:  R[168]=4    ;
  74:  IF R[136]=1,JMP LBL[16] ;
  75:  JMP LBL[15] ;
  76:   ;
  77:  LBL[14:WRONG INPUT] ;
  78:  MESSAGE[WRONG INPUT] ;
  79:  MESSAGE[PART LOST FAULT ...] ;
  80:  MESSAGE[RETRY IGNORE OR DROP...] ;
  81:  MESSAGE[WRONG INPUT FOR RECOVRY] ;
  82:  JMP LBL[10] ;
  83:   ;
  84:  !-------------------------------- ;
  85:  !CONTINUE DROP OR CANCEL ;
  86:  LBL[15:DROP_CONT/CANCEL] ;
  87:   ;
  88:  CALL PCUSTYN(2,162) ;
  89:   ;
  90:  SELECT R[162]=0,JMP LBL[17] ;
  91:         =1,JMP LBL[10] ;
  92:         ELSE,JMP LBL[18] ;
  93:   ;
  94:  LBL[16:REMOTE RESPONSE] ;
  95:  WAIT GI[72]>0    ;
  96:  R[162]=GI[72]    ;
  97:  SELECT R[162]=4,JMP LBL[17] ;
  98:         ELSE,JMP LBL[18] ;
  99:   ;
 100:  LBL[17:CONTINUE DROP] ;
 101:  IF R[102]=1,JMP LBL[100] ;
 102:  DROP PART    ;
 103:  R[154]=1    ;
 104:  R[167]=1    ;
 105:  CHK PART PRESENT(0) ;
 106:  JMP LBL[80] ;
 107:   ;
 108:  LBL[18:WRONG INPUT] ;
 109:  MESSAGE[...] ;
 110:  MESSAGE[PART LOST FAULT ...] ;
 111:  MESSAGE[CONT DROP OR CANCEL ...] ;
 112:  MESSAGE[WRONG INPUT FOR RECOVRY] ;
 113:  JMP LBL[13] ;
 114:   ;
 115:  !-------------------------------- ;
 116:  !PART_NOT_RELEASED FLT RECOVERY ;
 117:  !RETRY / DROP DIALOG ;
 118:  LBL[20:PRT NOT_RELS FLT] ;
 119:   ;
 120:  IF R[136]=1,JMP LBL[29] ;
 121:   ;
 122:  CALL PCUSTYN(1,162) ;
 123:   ;
 124:  SELECT R[162]=0,JMP LBL[21] ;
 125:         =1,JMP LBL[22] ;
 126:         ELSE,JMP LBL[23] ;
 127:   ;
 128:  LBL[29:REMOTE RESPONSE] ;
 129:  WAIT GI[72]>0    ;
 130:  R[162]=GI[72]    ;
 131:  SELECT R[162]=2,JMP LBL[21] ;
 132:         =4,JMP LBL[22] ;
 133:         ELSE,JMP LBL[23] ;
 134:   ;
 135:  LBL[21:RETRY] ;
 136:  R[167]=1    ;
 137:  CHK PART PRESENT(0) ;
 138:  IF R[147]=0,JMP LBL[20] ;
 139:  R[162]=0    ;
 140:  R[168]=2    ;
 141:  JMP LBL[80] ;
 142:   ;
 143:  LBL[22:DROP] ;
 144:  DROP PART    ;
 145:  R[167]=1    ;
 146:  CHK PART PRESENT(0) ;
 147:  IF R[147]=0,JMP LBL[20] ;
 148:  R[162]=0    ;
 149:  R[168]=4    ;
 150:  JMP LBL[80] ;
 151:   ;
 152:  LBL[23:WRONG INPUT] ;
 153:  MESSAGE[...] ;
 154:  MESSAGE[PART NOT RELEASD FLT...] ;
 155:  MESSAGE[RETRY OR DROP...] ;
 156:  MESSAGE[WRONG INPUT FOR RECOVRY] ;
 157:  JMP LBL[20] ;
 158:   ;
 159:  !-------------------------------- ;
 160:  !TOOL PICK FLT MSG & RECOVERY ;
 161:  LBL[30] ;
 162:  DO[337]=ON ;
 163:  IF R[136]=1,JMP LBL[39] ;
 164:   ;
 165:  CALL PCUSTYN(3,162) ;
 166:   ;
 167:  SELECT R[162]=0,JMP LBL[31] ;
 168:         =1,JMP LBL[32] ;
 169:         ELSE,JMP LBL[33] ;
 170:   ;
 171:  LBL[39:REMOTE RESPONSE] ;
 172:  WAIT GI[72]>0    ;
 173:  R[162]=GI[72]    ;
 174:  SELECT R[162]=2,JMP LBL[31] ;
 175:         =6,JMP LBL[32] ;
 176:         ELSE,JMP LBL[33] ;
 177:   ;
 178:  LBL[31:RETRY TOOL LATCH] ;
 179:  DO[18]=ON ;
 180:  R[167]=1    ;
 181:  LATCH TOOL    ;
 182:  IF R[170]=0,JMP LBL[30] ;
 183:  GO[71]=126 ;
 184:  R[162]=0    ;
 185:  R[168]=2    ;
 186:  JMP LBL[80] ;
 187:   ;
 188:  LBL[32:UNLTCH & MV HOME] ;
 189:  GO[71]=126 ;
 190:  DO[18]=ON ;
 191:  UNLATCH TOOL    ;
 192:  R[162]=0    ;
 193:  R[168]=6    ;
 194:  JMP LBL[80] ;
 195:   ;
 196:  LBL[33:WRONG INPUT] ;
 197:  MESSAGE[...] ;
 198:  MESSAGE[WRONG INPUT FOR RECOVRY] ;
 199:  JMP LBL[30] ;
 200:   ;
 201:  !-------------------------------- ;
 202:  !TOOL DROPOFF FLT MSG & RECOVERY ;
 203:  LBL[40] ;
 204:  DO[337]=ON ;
 205:  IF R[136]=1,JMP LBL[49] ;
 206:   ;
 207:  CALL PCUSTYN(4,162) ;
 208:   ;
 209:  SELECT R[162]=0,JMP LBL[41] ;
 210:         =1,JMP LBL[42] ;
 211:         ELSE,JMP LBL[43] ;
 212:   ;
 213:  LBL[49:REMOTE RESPONSE] ;
 214:  WAIT GI[72]>0    ;
 215:  R[162]=GI[72]    ;
 216:  SELECT R[162]=2,JMP LBL[41] ;
 217:         =7,JMP LBL[42] ;
 218:         ELSE,JMP LBL[43] ;
 219:   ;
 220:  LBL[41:RETRY TL UNLATCH] ;
 221:  DO[18]=ON ;
 222:  R[167]=1    ;
 223:  UNLATCH TOOL    ;
 224:  IF R[170]=0,JMP LBL[40] ;
 225:  GO[71]=126 ;
 226:  R[162]=0    ;
 227:  R[168]=2    ;
 228:  JMP LBL[80] ;
 229:   ;
 230:  LBL[42:LATCH & MOV HOME] ;
 231:  GO[71]=126 ;
 232:  DO[18]=ON ;
 233:  LATCH TOOL    ;
 234:  R[162]=0    ;
 235:  R[168]=7    ;
 236:  JMP LBL[80] ;
 237:   ;
 238:  LBL[43:WRONG INPUT] ;
 239:  MESSAGE[...] ;
 240:  MESSAGE[WRONG INPUT FOR RECOVRY] ;
 241:  JMP LBL[40] ;
 242:   ;
 243:  !-------------------------------- ;
 244:  !CONTINUE ROBOT MOTION ;
 245:  LBL[80:CONT ROBOT MOTN] ;
 246:  IF R[136]=1,JMP LBL[90] ;
 247:  CALL PCUSTOK(1,162) ;
 248:  GO[71]=127 ;
 249:  DO[523]=OFF ;
 250:  DO[337]=OFF ;
 251:  JMP LBL[100] ;
 252:   ;
 253:  !-------------------------------- ;
 254:  !CONT ROBOT MOTION - HMI INPUT ;
 255:  LBL[90:HMI CONT RBT MOV] ;
 256:  WAIT GI[72]=5    ;
 257:  WAIT   1.00(sec) ;
 258:  DO[523]=OFF ;
 259:  GO[71]=127 ;
 260:  JMP LBL[100] ;
 261:   ;
 262:  !-------------------------------- ;
 263:  !MATERIAL HANDLING ALERT ;
 264:  LBL[95:MH ALERT ] ;
 265:  DO[523]=OFF ;
 266:  DO[524]=PULSE,1.0sec ;
 267:  WAIT   1.00(sec) ;
 268:  GO[71]=0 ;
 269:  JMP LBL[100] ;
 270:   ;
 271:  !-------------------------------- ;
 272:  !M/H FAULT - NO AUTOMTC RECOVERY ;
 273:  LBL[97:MH FAULT] ;
 274:  DO[524]=OFF ;
 275:  DO[523]=PULSE,1.0sec ;
 276:  WAIT   1.00(sec) ;
 277:  GO[71]=0 ;
 278:  JMP LBL[100] ;
 279:   ;
 280:  !-------------------------------- ;
 281:  LBL[99] ;
 282:  ABORT ;
 283:   ;
 284:  !-------------------------------- ;
 285:  LBL[100:END OF PROGRAM] ;
 286:  R[162]=0    ;
 287:  R[163]=0    ;
 288:  R[167]=0    ;
/POS
/END
