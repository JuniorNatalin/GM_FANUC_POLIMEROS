/PROG  STYLE109_TC_R02
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TC DR STA35";
PROG_SIZE	= 2834;
CREATE		= DATE 19-03-05  TIME 18:33:10;
MODIFIED	= DATE 22-01-22  TIME 10:02:08;
FILE_NAME	= PRT101R1;
VERSION		= 0;
LINE_COUNT	= 121;
MEMORY_SIZE	= 3270;
PROTECT		= READ_WRITE;
STORAGE		= SHADOW;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  !* ROBOT ID: IM01R02B02 ;
   5:  !*    F-196844 ;
   6:  !* Line: Line B02 ;
   7:  !******************************** ;
   8:  !******************************** ;
   9:  ! STYLE109: PROGRAM FOR MULTI ;
  10:  !    TOOL DROP TO TOOL BUCK ;
  11:  ! TL BUCK = STATION 35 ;
  12:  !******************************** ;
  13:  !  2016-08-01 Global-4 S/W Rel 4. ;
  14:  !******************************** ;
  15:  ! Author- Esys Automation ;
  16:  !******************************** ;
  17:   ;
  18:  ! INITIALIZE ;
  19:  GO[1]=109 ;
  20:  R[14]=100    ;
  21:  R[139]=2    ;
  22:  R[140]=1    ;
  23:   ;
  24:  !-------------------------------- ;
  25:  LBL[10:TOOL CHECKS] ;
  26:   ;
  27:  !Check if Tool Present ;
  28:  R[133]=0    ;
  29:  CHK TOOL PRESENT(1) ;
  30:   ;
  31:  !Get Tool ID From PLC ;
  32:  R[156]=GI[15]    ;
  33:   ;
  34:  !Check if Correct Tool for Drop ;
  35:  R[133]=0    ;
  36:  CALL TL_CHK_KM(R[156]) ;
  37:   ;
  38:  !If TL CHK Pass, Cont to Pounce ;
  39:  IF R[133]=1,JMP LBL[20] ;
  40:   ;
  41:  !If TL CHK Fail, MHFault ;
  42:  JMP LBL[905] ;
  43:   ;
  44:  !-------------------------------- ;
  45:  LBL[20:MOVE TO POUNCE] ;
  46:   ;
  47:  ENTER I-ZONE(1) ;
  48:  ENTER I-ZONE(2) ;
  49:   ;
  50:  CALL POUNCE_STA35    ;
  51:   ;
  52:  ! Move to Home or Continue Cycle ;
  53:  SELECT R[150]=0,JMP LBL[30] ;
  54:         =15,JMP LBL[299] ;
  55:         ELSE,JMP LBL[20] ;
  56:   ;
  57:  !-------------------------------- ;
  58:  LBL[30:START TOOL DROP] ;
  59:   ;
  60:  !Drop Tool to station 35 ;
  61:  ! based on carried tool ;
  62:  IF (GI[15]>=1 AND GI[15]<=50) THEN ;
  63:  !Drop Tool Nest 35 ;
  64:  CALL S109_DR_TCOMUM35    ;
  65:  ELSE ;
  66:  MESSAGE[AGUARDA TOOL CORRETA] ;
  67:  WAIT GI[15]>=1 AND GI[15]<=50    ;
  68:  JMP LBL[30] ;
  69:  ENDIF ;
  70:   ;
  71:  LBL[299:Return Home] ;
  72:   ;
  73:  !SEG 63, Robot Move to Home ;
  74:  SET SEGMENT(63) ;
  75:   ;
  76:  CALL RET_HOME_STA35    ;
  77:   ;
  78:  EXIT I-ZONE(1) ;
  79:  EXIT I-ZONE(2) ;
  80:   ;
  81:  MOVE TO HOME    ;
  82:   ;
  83:  !Check if No Tool Present ;
  84:  R[133]=0    ;
  85:  CHK TOOL PRESENT(0) ;
  86:   ;
  87:  !Update Tool Information ;
  88:  R[157]=GI[15]    ;
  89:  GO[70]=0 ;
  90:  END ;
  91:   ;
  92:   ;
  93:  !*****FAULTS******** ;
  94:  LBL[905:Invalid Tool Num] ;
  95:  MESSAGE[...] ;
  96:  MESSAGE[TOOL CHANGER FAULT] ;
  97:  MESSAGE[ INVALID TOOL NUMBER] ;
  98:  MESSAGE[ GI[70] MUST BE ] ;
  99:  MESSAGE[ EQUAL GI[15] ] ;
 100:  DO[523]=ON ;
 101:  UALM[3] ;
 102:  DO[523]=OFF ;
 103:  JMP LBL[10] ;
 104:   ;
 105:  LBL[910:Invalid Code] ;
 106:  MESSAGE[...] ;
 107:  MESSAGE[TOOL CHANGER FAULT] ;
 108:  MESSAGE[ INVALID TOOL NUMBER] ;
 109:  MESSAGE[ R[156] NOT IN SELECT] ;
 110:  MESSAGE[ LIST.] ;
 111:  MESSAGE[ GI[70:NextToolID] ] ;
 112:  MESSAGE[ NOT EQUAL TO CARRIED TOOL] ;
 113:  MESSAGE[ CHECK PLC OR ] ;
 114:  MESSAGE[ TEACH DROP PATH FOR] ;
 115:  MESSAGE[ CARRIED TOOL.] ;
 116:  MESSAGE[...] ;
 117:  DO[523]=ON ;
 118:  UALM[3] ;
 119:  DO[523]=OFF ;
 120:  JMP LBL[10] ;
 121:   ;
/POS
/END
