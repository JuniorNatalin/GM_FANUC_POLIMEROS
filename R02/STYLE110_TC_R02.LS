/PROG  STYLE110_TC_R02
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TC PK STA35";
PROG_SIZE	= 2694;
CREATE		= DATE 19-03-05  TIME 18:33:46;
MODIFIED	= DATE 19-03-06  TIME 18:17:46;
FILE_NAME	= PRT103_R;
VERSION		= 0;
LINE_COUNT	= 117;
MEMORY_SIZE	= 3138;
PROTECT		= READ_WRITE;
STORAGE		= SHADOW;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  !* ROBOT ID: IM01R02B02 ;
   5:  !*    F-196844 ;
   6:  !* Line: Line B02 ;
   7:  !******************************** ;
   8:  !******************************** ;
   9:  ! STYLE110: PROGRAM FOR MULTI ;
  10:  !    TOOL PICK TO TO TOOL BUCK ;
  11:  ! TL BUCK = STATION 35 ;
  12:  !******************************** ;
  13:  !  2016-08-01 Global-4 S/W Rel 4. ;
  14:  !******************************** ;
  15:  ! Author- Esys Automation ;
  16:  !******************************** ;
  17:   ;
  18:  ! INITIALIZE ;
  19:  GO[1]=110 ;
  20:  R[14]=100    ;
  21:  R[139]=2    ;
  22:  R[140]=1    ;
  23:   ;
  24:  !-------------------------------- ;
  25:  LBL[10:TOOL CHECKS] ;
  26:   ;
  27:  !Check if No Tool Present ;
  28:  R[133]=0    ;
  29:  CHK TOOL PRESENT(0) ;
  30:   ;
  31:  !If No TL Present, Cont to Pounce ;
  32:  IF R[133]=1,JMP LBL[20] ;
  33:   ;
  34:  !If TL Present, MHFault ;
  35:  JMP LBL[905] ;
  36:   ;
  37:  !-------------------------------- ;
  38:  LBL[20:MOVE TO POUNCE] ;
  39:   ;
  40:  ENTER I-ZONE(1) ;
  41:  ENTER I-ZONE(2) ;
  42:   ;
  43:  CALL POUNCE_STA35    ;
  44:   ;
  45:  !Get Tool ID From PLC ;
  46:  R[156]=GI[70]    ;
  47:   ;
  48:  ! Move to Home or Continue Cycle ;
  49:  SELECT R[150]=0,JMP LBL[30] ;
  50:         =15,JMP LBL[299] ;
  51:         ELSE,JMP LBL[20] ;
  52:   ;
  53:  !-------------------------------- ;
  54:  LBL[30:START TOOL PICK] ;
  55:   ;
  56:  !Pick Tool from station 35 ;
  57:  ! based on carried tool ;
  58:  IF (GI[15]=255) THEN ;
  59:  !Pick Tool  STA 35 ;
  60:  CALL S110_PK_TCOMUM35    ;
  61:  ELSE ;
  62:  MESSAGE[AGUARDA ROBO SEM TOOL] ;
  63:  WAIT GI[15]=255    ;
  64:  JMP LBL[30] ;
  65:  ENDIF ;
  66:   ;
  67:  LBL[299:Return Home] ;
  68:   ;
  69:  !SEG 63, Robot Move to Home ;
  70:  SET SEGMENT(63) ;
  71:   ;
  72:  CALL RET_HOME_STA35    ;
  73:   ;
  74:  EXIT I-ZONE(1) ;
  75:  EXIT I-ZONE(2) ;
  76:   ;
  77:  MOVE TO HOME    ;
  78:   ;
  79:   ;
  80:  !Check if Tool Present ;
  81:  R[133]=0    ;
  82:  CHK TOOL PRESENT(1) ;
  83:   ;
  84:  !Update Tool Information ;
  85:  R[157]=GI[15]    ;
  86:  GO[70]=1 ;
  87:  END ;
  88:   ;
  89:   ;
  90:  !*****FAULTS******** ;
  91:  LBL[905:Invalid Tool Num] ;
  92:  MESSAGE[...] ;
  93:  MESSAGE[TOOL CHANGER FAULT] ;
  94:  MESSAGE[ INVALID TOOL NUMBER] ;
  95:  MESSAGE[ TOOL LATCHED] ;
  96:  MESSAGE[ GI[15] MUST ] ;
  97:  MESSAGE[ EQUAL 255 ] ;
  98:  DO[523]=ON ;
  99:  UALM[3] ;
 100:  DO[523]=OFF ;
 101:  JMP LBL[10] ;
 102:   ;
 103:  LBL[910:Invalid Code] ;
 104:  MESSAGE[...] ;
 105:  MESSAGE[TOOL CHANGER FAULT] ;
 106:  MESSAGE[ INVALID TOOL NUMBER] ;
 107:  MESSAGE[ R[156] NOT IN SELECT] ;
 108:  MESSAGE[ LIST.] ;
 109:  MESSAGE[ CHECK PLC OR ] ;
 110:  MESSAGE[ TEACH PICK PATH FOR] ;
 111:  MESSAGE[ CARRIED TOOL.] ;
 112:  MESSAGE[...] ;
 113:  DO[523]=ON ;
 114:  UALM[3] ;
 115:  DO[523]=OFF ;
 116:  JMP LBL[10] ;
 117:   ;
/POS
/END
