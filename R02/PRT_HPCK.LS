/PROG  PRT_HPCK	  Macro
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "PICK PART";
PROG_SIZE	= 2636;
CREATE		= DATE 06-12-07  TIME 07:28:00;
MODIFIED	= DATE 16-10-26  TIME 13:52:04;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 103;
MEMORY_SIZE	= 3120;
PROTECT		= READ;
STORAGE		= CMOS;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
  HANDLING : TRUE ; 
/MN
   1:  !******************************** ;
   2:  ! GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  ! PRT_HPCK:  MACRO  HMI PART PICK ;
   5:  ! THIS MACRO  IS EXECUTED IN THE ;
   6:  ! MACRO TABLE USING DI[473]. ;
   7:  ! THIS HMI FUNCTION IS USED FOR ;
   8:  ! TOOL ADJUST. ROBOT MUST BE AT ;
   9:  ! PICK OR DROP POSITION. IT ... ;
  10:  !   1) TURNS BLOWOFF OFF AND ;
  11:  !      TURNS VACUUM ON, OR ;
  12:  !   2) CLOSES GRIPPERS, OR ;
  13:  !   3) DOES BOTH AS APPROPRIATE. ;
  14:  !******************************** ;
  15:  ! 2016-10-25 Global-4 S/W REL 4.0 ;
  16:  !******************************** ;
  17:   ;
  18:  CALL DMSTAT    ;
  19:   ;
  20:  SELECT R[142]=1,JMP LBL[10] ;
  21:         =2,JMP LBL[60] ;
  22:         =3,JMP LBL[10] ;
  23:         ELSE,JMP LBL[97] ;
  24:   ;
  25:  !******************************** ;
  26:  LBL[10:VACUUM ...] ;
  27:   ;
  28:  SELECT R[155]=1,JMP LBL[20] ;
  29:         =2,JMP LBL[30] ;
  30:         ELSE,JMP LBL[99] ;
  31:   ;
  32:  !-------------------------------- ;
  33:  LBL[20:VENTURI VACUUM] ;
  34:   ;
  35:  !--- Vacuum on Side 1 -------- ;
  36:  IF R[148]=1 OR DI[282]=ON,JMP LBL[25] ;
  37:  DO[806]=OFF ;
  38:  DO[805]=ON ;
  39:   ;
  40:  LBL[25] ;
  41:  !--- Vacuum on Side 2 -------- ;
  42:  IF R[149]=1 OR DI[283]=ON,JMP LBL[59] ;
  43:  DO[808]=OFF ;
  44:  DO[807]=ON ;
  45:  JMP LBL[59] ;
  46:   ;
  47:  !-------------------------------- ;
  48:  LBL[40:SMART VAC PUMP] ;
  49:   ;
  50:  !--- Vacuum on Side 1 -------- ;
  51:  IF R[148]=1 OR DI[282]=ON,JMP LBL[45] ;
  52:  DO[866]=OFF ;
  53:  DO[865]=ON ;
  54:   ;
  55:  LBL[45] ;
  56:  !--- Vacuum on Side 2 -------- ;
  57:  IF R[149]=1 OR DI[283]=ON,JMP LBL[59] ;
  58:  DO[868]=OFF ;
  59:  DO[867]=ON ;
  60:   ;
  61:  !-------------------------------- ;
  62:  LBL[59:End of Vacuum] ;
  63:  IF R[142]=3,JMP LBL[60] ;
  64:  JMP LBL[100] ;
  65:   ;
  66:  !******************************** ;
  67:  LBL[60:GRIPPERS ...] ;
  68:   ;
  69:  !R[145]=0: Grippers Not Supported ;
  70:  IF R[145]=0,JMP LBL[100] ;
  71:   ;
  72:  IF R[148]=1 OR DI[282]=ON,JMP LBL[65] ;
  73:  ! --- Grippers On Side-1 ------ ;
  74:  DO[802]=OFF ;
  75:  DO[801]=ON ;
  76:   ;
  77:  LBL[65] ;
  78:  IF R[149]=1 OR DI[283]=ON,JMP LBL[100] ;
  79:  ! --- Grippers On Side-2 ------ ;
  80:  DO[804]=OFF ;
  81:  DO[803]=ON ;
  82:  JMP LBL[100] ;
  83:   ;
  84:  !******************************** ;
  85:  LBL[97:ABORT-WRONG PARM] ;
  86:  MESSAGE[...] ;
  87:  MESSAGE[PRT_CHK: WRONG VALUE IN] ;
  88:  MESSAGE[R[155] FOR VACUUM TYPE] ;
  89:  MESSAGE[SET 1 FOR VENTURI AND] ;
  90:  MESSAGE[SET 2 FOR SMART PUMP] ;
  91:  ABORT ;
  92:   ;
  93:  !-------------------------------- ;
  94:  LBL[99:ABORT WRONG ARGM] ;
  95:  MESSAGE[PROGRAM ABORTED ...] ;
  96:  MESSAGE[WRONG ARGUM IN PRT_PICK_HMI] ;
  97:  ABORT ;
  98:   ;
  99:  !-------------------------------- ;
 100:  LBL[100:END OF MACRO] ;
 101:   ;
 102:  DO[473]=PULSE,2.0sec ;
 103:   ;
/POS
/END
