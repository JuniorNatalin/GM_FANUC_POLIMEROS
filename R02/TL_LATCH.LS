/PROG  TL_LATCH	  Macro
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "LATCH TOOL PLATE";
PROG_SIZE	= 2774;
CREATE		= DATE 06-12-18  TIME 07:34:08;
MODIFIED	= DATE 16-10-26  TIME 11:32:58;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 118;
MEMORY_SIZE	= 3206;
PROTECT		= READ;
STORAGE		= CMOS;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  !******************************** ;
   2:  !  GM METAL FORMING GLOBAL 4 S/W ;
   3:  !******************************** ;
   4:  ! TL_LATCH: MACRO FOR LATCHING ;
   5:  !    THE TOOL PLATE. ;
   6:  !******************************** ;
   7:  ! 2016-10-26 Global-4 S/W Rel 4.0 ;
   8:  !******************************** ;
   9:   ;
  10:  CALL DMSTAT    ;
  11:   ;
  12:  R[136]=DI[276]    ;
  13:   ;
  14:  LBL[10] ;
  15:  IF R[141]=1 AND DI[2008]=OFF,JMP LBL[50] ;
  16:   ;
  17:  IF (!SI[9]),CALL CUSTOOK(20) ;
  18:  WAIT DI[909]=ON TIMEOUT,LBL[70] ;
  19:   ;
  20:  WAIT   1.00(sec) ;
  21:   ;
  22:  IF (DI[907]),DO[905]=PULSE,1.0sec ;
  23:  WAIT   2.50(sec) ;
  24:   ;
  25:  IF DI[905]=ON AND DI[906]=OFF,JMP LBL[90] ;
  26:   ;
  27:  !================================ ;
  28:  ! UNABLE TO LATCH FAULT ;
  29:  MESSAGE[...] ;
  30:  MESSAGE[TL_LATCH: TC FAULT ...] ;
  31:  MESSAGE[ UNABLE TO LATCH.] ;
  32:  MESSAGE[ NEED DI[905] HIGH] ;
  33:  MESSAGE[ AND DI[906] LOW.] ;
  34:   ;
  35:  R[170]=0    ;
  36:  IF GO[71]=126,JMP LBL[99] ;
  37:   ;
  38:  IF R[167]=1,JMP LBL[100] ;
  39:  JMP LBL[60] ;
  40:   ;
  41:  !================================ ;
  42:  ! AUX POWER FAULT ;
  43:  LBL[50:AUX POWER FAULT] ;
  44:  MESSAGE[...] ;
  45:  MESSAGE[TL_LATCH: TC FAULT ...] ;
  46:  MESSAGE[ AUX POWER FAULT -] ;
  47:  MESSAGE[ NO AUX POWER AT ROBOT ] ;
  48:  MESSAGE[ SIDE OF TOOL CHANGER.] ;
  49:  MESSAGE[ NEED DI[2008] HIGH.] ;
  50:   ;
  51:  R[170]=0    ;
  52:  DO[907]=PULSE,0.5sec ;
  53:   ;
  54:  IF GO[71]=126,JMP LBL[99] ;
  55:   ;
  56:  IF R[167]=1,JMP LBL[100] ;
  57:  JMP LBL[60] ;
  58:   ;
  59:  !================================ ;
  60:  LBL[60:FAULT RECOVERY] ;
  61:  SELECT R[136]=1,JMP LBL[61] ;
  62:         =0,JMP LBL[62] ;
  63:         ELSE,JMP LBL[60] ;
  64:   ;
  65:  !-------------------------------- ;
  66:  LBL[61:HMI RECOVERY] ;
  67:  MESSAGE[HMI FAULT RECOVERY] ;
  68:  MH FAULT RECOVRY(111,1,3) ;
  69:  JMP LBL[100] ;
  70:   ;
  71:  !-------------------------------- ;
  72:  LBL[62:TP RECOVERY] ;
  73:  MESSAGE[TP FAULT RECOVERY] ;
  74:  MH FAULT RECOVRY(112,1,3) ;
  75:  JMP LBL[100] ;
  76:   ;
  77:  !================================ ;
  78:  LBL[70:TL LTCH FLT RCVR] ;
  79:  MESSAGE[TL_LATCH: LATCHING FLT.] ;
  80:  MESSAGE[TOOL PLATE NOT FOUND...] ;
  81:  MESSAGE[DI[909] MUST BE ON AT] ;
  82:  MESSAGE[TOOL PICK POSITION.] ;
  83:   ;
  84:  DO[907]=PULSE,0.5sec ;
  85:   ;
  86:  SELECT R[136]=1,JMP LBL[71] ;
  87:         =0,JMP LBL[72] ;
  88:         ELSE,JMP LBL[70] ;
  89:   ;
  90:  !-------------------------------- ;
  91:  LBL[71:HMI RECOVERY] ;
  92:  MESSAGE[HMI FAULT RECOVERY] ;
  93:  MH FAULT RECOVRY(111,1,3) ;
  94:  JMP LBL[100] ;
  95:   ;
  96:  !-------------------------------- ;
  97:  LBL[72:TP RECOVERY] ;
  98:  MESSAGE[TP FAULT RECOVERY] ;
  99:  MH FAULT RECOVRY(112,1,3) ;
 100:  JMP LBL[100] ;
 101:   ;
 102:  !================================ ;
 103:  LBL[90:TOOL LATCH OK] ;
 104:  DO[338]=ON ;
 105:  IF GO[71]=126,JMP LBL[100] ;
 106:  R[170]=1    ;
 107:  JMP LBL[100] ;
 108:   ;
 109:  !================================ ;
 110:  LBL[99:ABORT] ;
 111:  MESSAGE[TL_LATCH: LATCHING FLT] ;
 112:  MESSAGE[UNABLE TO RECOVER] ;
 113:  MESSAGE[ABORT PROGRAM ...] ;
 114:  MH FAULT RECOVRY(116,1,0) ;
 115:  DO[907]=PULSE,0.5sec ;
 116:  ABORT ;
 117:   ;
 118:  LBL[100:END OF PROGRAM] ;
/POS
/END
